// Initialization of variables, for example, before transformation.
pre {
	// Controls whether debug information is printed.
	var debug = true;
	var emfTool = new Native("org.eclipse.epsilon.emc.emf.tools.EmfTool");
	var ecoreUtil = emfTool.ecoreUtil;
	
}


pattern split_conditional
	pkg : Source!RCPackage
		guard: pkg.name.asString().startsWith(clonepackagename),
	smb: Source!StateMachineBody from: pkg.machines     
	{
	match: smb.name.equals(smbP)
	do { 
	
		//Getting the source node
		var src = smb.nodes.selectOne(s|s.name.equals(srcP));
		//Getting the first transition (with [g])
		var tr1 = smb.transitions.selectOne(t|t.name.equals(tr1P) and t.source = src);
	        //Getting the junction that is the target of tr1
		var junction1 = smb.nodes.selectOne(j|j=tr1.target);
		//Getting the transition whose source is junction1
		var trThen = smb.transitions.selectOne(t|t.source=junction1);
	    
	        //Getting the second transition (with [not g])
		var tr2 = smb.transitions.selectOne(t|t.name.equals(tr2P));
	        //Getting the junction that is the target of tr2
		var junction2 = smb.nodes.selectOne(j|j=tr2.target);
		//Getting the transition whose source is junction2
		var trElse = smb.transitions.selectOne(t|t.source=junction2);
	    
	    //Creating the IfStmt
	    var ifStmt = new Source!IfStmt();
	    ifStmt.expression = tr1.condition; 
	    ifStmt.then = trThen.action;
	    ifStmt.`else` = trElse.action;
	    
	    //Getting the target node
	    var tgtNode = smb.nodes.selectOne(s|s.name.equals(tgtP));
	    //Updating the target of the first transition
	    tr1.target = tgtNode;
	    //Updating the action of the first transition
	    tr1.action = ifStmt;
	    
	    //Removing the junctions junction1 and junction2 and the transitions
	    smb.nodes.remove(junction1);
	    smb.nodes.remove(junction2);
	    smb.transitions.remove(trThen);
	    smb.transitions.remove(trElse);
	    smb.transitions.remove(tr2);
    
	}
}
